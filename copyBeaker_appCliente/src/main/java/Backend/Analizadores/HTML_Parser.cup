/*1. package e imports*/
package Backend.Analizadores;

import java_cup.runtime.*;
import java.util.List;
import java.util.ArrayList;
import Backend.Objetos.Token;
import Backend.Manejadores.ManejadorErrores;
import Backend.Objetos.Error;
import Backend.Objetos.Enums.SintaxError;
import Backend.Analizadores.HTML_ParserSym.*;

class HTML_Parser;

action code{://Métodos que NO pueden ser accedidos desde el exterior, puesto que forman parte de una clase interna




    private void addError(Token token, int linea, int columna, SintaxError error){//tipoReporte = operacion o error                
        manejadorErrores.setError((new Error(((token != null)?token.getLexema():""), String.valueOf(linea), String.valueOf(columna), error)));
        
    }
:}/*el código que se encuentra aquí dentro NO puede ser accedido desde la clase...*/

parser code {:/*código que modifica el comportamiento del parser [sobreescribiendo o creado métodos]    */
    private ManejadorErrores manejadorErrores;        

    public HTML_Parser(HTML_Lexer lexer, ManejadorErrores manejadorErroresLex){/*nuevo constructor*/
        super(lexer);        

        manejadorErrores = manejadorErroresLex;        
    }    

    public void report_fatal_error(String message, Object info){
        manejadorErrores.setError(new Error(((info != null)?info.toString():""), "", 
        "", SintaxError.FATAL_ERROR));
    }

    protected int error_sync_size(){
        return 1;
    }/*para que establezca que con un token bien leido basta para recuperarse...   */
:}/*este si puede ser accedido desde el código...*/

/*2. terminales*/

terminal HTML_A, HTML_C, H1_A, H1_C, H2_A, H2_C, TABLE_A, TABLE_C, TR_A, TR_C, TH_A, TH_C, TD_A, TD_C, BR, FOR_C;
terminal FOR_A, FOR_CA, VAR_A, VAR_C;
terminal CORCHETE_A, CORCHETE_C, PUNTO;
terminal VARIABLE, ENTERO, CONTENIDO;//aquí no es nec un T = CADENA... xD
terminal RESULT, SCORE, CLASES, NOMBRE, VARIABLES, TIPO, FUNCION, METODOS, PARAMETROS, COMENTARIOS, TEXTO;
terminal ITERADOR, HASTA, DOS_PUNTOS;

/*3. no terminales*/
non terminal inicio, body;
non terminal etiqueta, headers, header1, rest_header1, header2, rest_header2, table, rest_table, for_loop, cuerpo_for_loop, rest_for_loop;
non terminal for_row, rest_for_row, table_row, cuerpo_for_row, rest_table_row, for_cell, cuerpo_for_cell, rest_for_cell;
non terminal table_head, rest_table_head, table_cell, rest_table_cell;
non terminal content, valor_variable, tipo_result, result_integer, cuerpo_result_integer;
non terminal result_string, valor_result, result_clases, cuerpo_clase, result_metodos, cuerpo_metodo;
non terminal result_variables, cuerpo_variable, result_comentarios, cuerpo_comentario, valor_numerico, var_tipo_numerica;
/*3.1 precedencia y asociatividad*/
/*no necesaria en este contexto*/

/*4. reglas de producción*/
start with inicio;

inicio ::= HTML_A body HTML_C
         | HTML_A HTML_C
         | error:err                                                                    {:System.out.println("[S] error: BAD_GENERAL_HTML_STRUCT");
                                                                                          addError(Token.parseToken(err), errleft, errright, SintaxError.HTML_BAD_GENERAL_STRUCT);:}
         ;//Aquí no es nec colocar un labda, porque por la separación de la entrada orignal, hecha en Java, si no se encuentra una <HTML> entonces no deuvlve un índice y por lo tanto no recorta nada

body ::= body etiqueta
       | body content
       | etiqueta
       | content
 //      | error
       ;

etiqueta ::= headers
           | table
           | for_loop
           | BR
           //| error
           ;

headers ::= header1
          | header2          
          | error:err                                                                  {:System.out.println("[S] error: BAD_HEADER_LABEL_STRUCT");
                                                                                         addError(Token.parseToken(err), errleft, errright, SintaxError.HTML_BAD_HEADER_LABEL_STRUCT);:}
          ;

header1 ::= H1_A rest_header1
      //    | error
          ;

rest_header1 ::= etiqueta rest_header1
               | content rest_header1
               | H1_C
               ;

header2 ::= H2_A rest_header2
      //    | error
          ;

rest_header2 ::= etiqueta rest_header2
               | content rest_header2
               | H2_C
               ;

table ::= TABLE_A rest_table
  //      | error
        ;

rest_table ::= headers rest_table
             | table rest_table
             | BR rest_table
             | content rest_table
             | for_row rest_table
             | table_row rest_table
             | TABLE_C
             ;

for_loop ::= FOR_A cuerpo_for_loop
            ;

cuerpo_for_loop ::= ITERADOR DOS_PUNTOS VARIABLE HASTA DOS_PUNTOS var_tipo_numerica FOR_CA rest_for_loop
           | error:err                                                                                                      {:System.out.println("[S] error: BAD_FOR_LOOP_BODY");
                                                                                                                              addError(Token.parseToken(err), errleft, errright, SintaxError.HTML_BAD_FOR_LOOP_BODY);:}
           ;

rest_for_loop ::= etiqueta rest_for_loop
                | content rest_for_loop
                | FOR_CA
                //| error
                ;

content ::= VAR_A valor_variable VAR_C
          | CONTENIDO
      //    | error
          ;

for_row ::= FOR_A cuerpo_for_row
          ;

cuerpo_for_row ::=  ITERADOR DOS_PUNTOS VARIABLE HASTA DOS_PUNTOS var_tipo_numerica FOR_CA rest_for_row
                 | error:err                                                                                                 {:System.out.println("[S] error: BAD_FOR_ROW_LOOP_BODY");
                                                                                                                               addError(Token.parseToken(err), errleft, errright, SintaxError.HTML_BAD_FOR_ROW_LOOP_BODY);:}
                 ;

rest_for_row ::= headers rest_for_row
               | table rest_for_row
               | BR rest_for_row
               | content rest_for_row
               | for_row rest_for_row
               | table_row rest_for_row
               | FOR_C
               ;
               
table_row ::= TR_A rest_table_row
        //    | error
            ;

rest_table_row ::= headers rest_table_row
                 | table rest_table_row
                 | BR rest_table_row
                 | content rest_table_row
                 | for_cell rest_table_row
                 | table_head rest_table_row
                 | table_cell rest_table_row
                 | TR_C
                 ;

for_cell ::= FOR_A cuerpo_for_cell
           ;

cuerpo_for_cell ::= ITERADOR DOS_PUNTOS VARIABLE HASTA DOS_PUNTOS var_tipo_numerica FOR_CA rest_for_cell
                  | error:err                                                                                                   {:System.out.println("[S] error: BAD_FOR_CELL_LOOP_STRUCT");
                                                                                                                                  addError(Token.parseToken(err), errleft, errright, SintaxError.HTML_BAD_FOR_CELL_LOOP_STRUCT);:}
                  ;
            
rest_for_cell ::= headers rest_for_cell
                | table rest_for_cell
                | BR rest_for_cell
                | content rest_for_cell
                | table_head rest_for_cell
                | table_cell rest_for_cell
                | for_cell rest_for_cell
                | FOR_C
                ;

table_head ::= TH_A rest_table_head
        //     | error
             ;
    
rest_table_head ::= etiqueta rest_table_head
                  | content rest_table_head
                  | TH_C
                  ;

table_cell ::= TD_A rest_table_cell
       //      | error
             ;

rest_table_cell ::= etiqueta rest_table_cell
                  | content rest_table_cell
                  | TD_C
                  ;
                  
valor_variable ::= tipo_result
                 | VARIABLE
                 | error:err                                                        {:System.out.println("[S] error: BAD_VARIABLE_VALUE");
                                                                                      addError(Token.parseToken(err), errleft, errright, SintaxError.HTML_BAD_VARIABLE_VALUE);:}
                 ;


tipo_result ::= result_integer
              | result_string
              ;

result_string ::= RESULT PUNTO valor_result
        //        | error
                ;

valor_result ::= SCORE
               | result_clases
               | result_metodos
               | result_variables               
               | result_comentarios
               | error:err                                                            {:System.out.println("[S] error: BAD_RESULT_INVOCATION");
                                                                                        addError(Token.parseToken(err), errleft, errright, SintaxError.HTML_BAD_RESULT_INVOCATION);:}
               ;

result_clases ::= CLASES 
                | CLASES cuerpo_clase
                ;


cuerpo_clase ::= CORCHETE_A valor_numerico CORCHETE_C
               | CORCHETE_A valor_numerico CORCHETE_C PUNTO NOMBRE
               | error:err                                                             {:System.out.println("[S] error: BAD_ATTRIBUTES_CLASS_INVOCATION");
                                                                                         addError(Token.parseToken(err), errleft, errright, SintaxError.HTML_BAD_ATTRIBUTES_CLASS_INVOCATION);:}
               ;

result_metodos ::= METODOS
                 | METODOS cuerpo_metodo
                 ;


cuerpo_metodo ::= CORCHETE_A valor_numerico CORCHETE_C
                | CORCHETE_A valor_numerico CORCHETE_C PUNTO TIPO
                | CORCHETE_A valor_numerico CORCHETE_C PUNTO NOMBRE                 
                | error:err                                                             {:System.out.println("[S] error: BAD_ATTRIBUTES_METHOD_INVOCATION");
                                                                                          addError(Token.parseToken(err), errleft, errright, SintaxError.HTML_BAD_ATTRIBUTES_METHOD_INVOCATION);:}
                ;

result_variables ::= VARIABLES
                   | VARIABLES cuerpo_variable
                   ;


cuerpo_variable ::= CORCHETE_A valor_numerico CORCHETE_C
                  | CORCHETE_A valor_numerico CORCHETE_C PUNTO TIPO
                  | CORCHETE_A valor_numerico CORCHETE_C PUNTO NOMBRE
                  | CORCHETE_A valor_numerico CORCHETE_C PUNTO FUNCION
                  | error:err                                                           {:System.out.println("[S] error: BAD_ATTRIBUTES_VAR_INVOCATION");
                                                                                          addError(Token.parseToken(err), errleft, errright, SintaxError.HTML_BAD_ATTRIBUTES_VAR_INVOCATION);:}
                  ;

result_comentarios ::= COMENTARIOS
                     | COMENTARIOS cuerpo_comentario
                     ;


cuerpo_comentario ::= CORCHETE_A valor_numerico CORCHETE_C
                    | CORCHETE_A valor_numerico CORCHETE_C PUNTO TEXTO
                     | error:err                                                        {:System.out.println("[S] error: BAD_ATTRIBUTE_COMMENT_INVOCATION");
                                                                                          addError(Token.parseToken(err), errleft, errright, SintaxError.HTML_BAD_ATTRIBUTE_COMMENT_INVOCATION);:}
                    ;

result_integer ::= RESULT cuerpo_result_integer
                 ;

cuerpo_result_integer ::= PUNTO METODOS CORCHETE_A valor_numerico CORCHETE_C PUNTO PARAMETROS
                        | error:err                                                                                 {:System.out.println("[S] error: BAD_INTEGER_RESULT_INVOCATION");
                                                                                                                      addError(Token.parseToken(err), errleft, errright, SintaxError.HTML_BAD_INTEGER_RESULT_INVOCATION);:}
                        ;

valor_numerico ::= ENTERO
                 | var_tipo_numerica
                 | error:err                                            {:System.out.println("[S] error: BAD_NUMBER_VALUE_STRUCT");
                                                                          addError(Token.parseToken(err), errleft, errright, SintaxError.HTML_BAD_NUMBER_VALUE_STRUCT);:}
                 ;


var_tipo_numerica ::= result_integer
                    | VARIABLE
                    ;

